Class {
	#name : #GLMTextMorphicTest,
	#superclass : #GLMMorphicTest,
	#category : #'Glamour-Tests-Morphic'
}

{ #category : #tests }
GLMTextMorphicTest >> testExplicitAcceptDoesNotAffectTextPort [
	| browser textMorph textPresentation |
	browser := GLMTabulator new.
	browser column: #one.
	(browser transmit)
		to: #one;
		andShow: [ :a | a text display: '123' ].
	window := browser openOn: 4.
	textMorph := self find: PluggableTextMorph in: window.
	textMorph simulateClick.
	self simulateKeyStrokes: '4'.
	self assert: (browser panes first port: #text) value = '1234'.
	self simulateKeyStroke: $s command.
	self simulateKeyStrokes: '56'.
	self assert: (browser panes first port: #text) value = '123456'
]

{ #category : #tests }
GLMTextMorphicTest >> testMultipleSelectionWithChange [
	| browser textMorph textPresentation |
	browser := GLMTabulator new.
	browser column: #one.
	(browser transmit)
		to: #one;
		andShow: [ :a | a text display: '123456789' ].
	window := browser openOn: 4.
	textMorph := self find: PluggableTextMorph in: window.
	self assert: textMorph selectionInterval isEmpty.
	textPresentation := (browser paneNamed: #one) presentations first.
	textPresentation selectionInterval: (2 to: 5).
	self assert: textMorph selectedContents asString equals: '2345'.
	self assert: textPresentation selectedText equals: '2345'
]

{ #category : #tests }
GLMTextMorphicTest >> testPastingUpdatesTextPort [
	| browser textMorph textPresentation |
	browser := GLMTabulator new.
	browser column: #one.
	(browser transmit)
		to: #one;
		andShow: [ :a | a text display: '' ].
	window := browser openOn: 42.
	textMorph := self find: PluggableTextMorph in: window.

	Clipboard clipboardText: '42'.
	textMorph simulateClick.
	self simulateKeyStroke: $v command.
	self assert: (browser panes first port: #text) value = '42'
]

{ #category : #tests }
GLMTextMorphicTest >> testUnregisterFromAnnouncementsWhenUpdating [
	| browser announcingCollection numberOfExecutions |
	numberOfExecutions := 0.
	announcingCollection := GLMAnnouncingCollection new.
	browser := GLMTabulator new.
	browser column: #text.
	(browser transmit)
		to: #text;
		andShow: [ :a | 
					(a text)
						updateOn: GLMItemAdded from: [ announcingCollection ];
						display: [ numberOfExecutions := numberOfExecutions + 1 ] ].
	window := browser openOn: announcingCollection.
	1 to: 3 do: [ :each | announcingCollection add: each ].
	self assert: numberOfExecutions equals: 4	"it should not be 8"
]
